Workflow WordCountWorkflow {
	
	streams {
		Queue Lines {
			type Line
			inputOf (WordCounter)
			outputOf (LineSource)
		}
		Queue WordFrequencies {
			type WordFrequency
			inputOf (Filter)
			outputOf (WordCounter)
		}
		Queue Filtered {
			type WordFrequency
			inputOf (WordCountSink)
			outputOf (Filter)
		}
	}// streams
	
	tasks {
		masterOnly Source LineSource {
			output (Lines)
			languages(Java)
		}
		multipleOutputs Task WordCounter {
			input (Lines)
			output (WordFrequencies)
			languages(Java)
		}
		Task Filter {
			input (WordFrequencies)
			output (Filtered)
			languages(Python)
		}
		masterOnly Sink WordCountSink {
			input (Filtered)
			languages(Java)
		}
	}// tasks
	
	types {
		Type Line {
			fields {
				Field text {
					type String
				}
			}
		}
		Type WordFrequency {
			fields {
				Field word {
					type String
				}
				Field frequency {
					type int
				}
			}
		}
	}// types
	
	languages {
		Language Java {
			package "org.eclipse.scava.crossflow.examples.wordcount.xtext"
			outputFolder "src"
			genOutputFolder "src-gen"
		}
		Language Python {
			package "org.eclipse.scava.crossflow.examples.wordcount.xtext"
			outputFolder "py"
			genOutputFolder "py-gen"
			parameters {
				Parameter module {
					value default
				}
			}
		}
	}// languages
	
}